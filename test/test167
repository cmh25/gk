show:{[t]                                 / show
 if[(@*t[])|1<#?#'t[]                     / if not a table
  :((-|/#'$k)$k:!t),/" : ",/5:'t[]]       / use dict display
 i:(^t[])1                                / count
 j:@[t;nul;{(1|i)#x}]                     / empty
 f:{$[4:x;x;1_,/" ",/x]}                  / nested
 k:f''+$+j[]                              / data
 l:+,$!j                                  / labels
 m:1 -1(4:'t[])lin 0 -4                   / data justifications
 n:(|/#')'{x,y}'[l;k]                     / widths
 l:" ",//(-n)$l                           / padded labels
 k:" ",//(m*n)$k                          / padded data
 o:+," ",/n#\"-"                          / separators
 p:&-2=4:'t[]                             / where float
 p@:&{|/"."in/x}'k p                      / where decimals
 q:(*&)''"."=k p                          / where decimals in
 r:q-&/'q dv\0                            / rotation matrix
 k:.[k;(p;);{y!x};r]                      / align on decimals
 t:{x,y,z}'[l;o;k]                        / pre result
 s:{,/t[;x]}'!#*t                         / result
 s}                                       / matrix

//show:{`0:,," ";{`0:x,,,"\n"}:[~(4:x)_in 0 5;,5:x;5 5~4::'x;{x,'" ",'@[(#x)#"|";1;:;"+"],'y}. show_'x;show_ x]}

t:.()
t.a:10+!10
t.b:20+!10
t.c:30+!10
t.d:10.1+!10
t.e:20.2+!10
t.f:30.3+!10
t.g:10#"abcdefghijklmnopqrstuvwxyz"
t.h:`$/10#"abcdefghijklmnopqrstuvwxyz"
t.i:10#,!3
t.j:10#,0.1+!3
t.k:10#,3#"abcdefghijklmnopqrstuvwxyz"

`0:show t

\\
